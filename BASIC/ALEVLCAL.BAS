10       REM  PROGRAM TO COMPUTE VOLUME LEVEL TABLE VALUES.       
20       REM  RESULTS SENT TO SCREEN AND EITHER: COM1 OR FILE (VCALEVLS).
30       REM  RESULTS ARE GIVEN DIRECTLY IN 68000 SOURCE FORMAT.
40       REM  COMPUTES SCALE FACTORS FOR EACH OF VOLUME SETTINGS 1-31.
50       REM  SETTING 0 IS A SPECIAL CASE DONE W/O COMPUTATION.
60       REM  IN USE, SCALE FACTOR SCALES RAW VCA ENVELOPE AT EACH UPDATE TIME.
70      REM  ONLY 12 M.S.BITS OF VCA CONTROL OUTPUT WORD ARE SIGNIFICANT -
80      REM  HOWEVER, CONTROL VALUE AND SCALE FACTORS ARE MAINTAINED AT
90      REM  16-BITS TO PROVIDE SUFFICIENT LEVEL RESOLUTION.
100       PRINT "Transmit results over COM1?" 
110      INPUT " ( 0 = no, 1 = yes) ... : ",TRANS% 
120      IF TRANS% THEN OPEN "COM1:9600,N,8" AS #1 ELSE OPEN "O",#1,"VCALEVLS"
130      IF TRANS%=0 THEN PRINT "Results will be saved in file VCALEVLS -"
140      INPUT "Enter maximum attenuation (dB) - for level = 1: ",MAXDB#
150      STEPDB# = MAXDB#/30            
160     PRINT "             DC.W        0           ;  0    INF.    0.00000000"
170     PRINT #1,"             DC.W        0           ;  0    INF.    0.00000000"
180     FOR LEVELSETTING% = 1 TO 31
190     LEVELDB# = (31-LEVELSETTING%)*STEPDB#
200     LEVELLOG# = (LEVELDB#/10)*LOG(10)
210     LEVELFACTOR# = EXP(-LEVELLOG#)
220     SCALEDFACTOR# = LEVELFACTOR#*65535!
230     PRINT "             DC.W    ";
240     PRINT USING "#####           ";SCALEDFACTOR#;
250     PRINT "; ";
260     PRINT USING "##    ";LEVELSETTING%;
270     PRINT USING "##.#    ";LEVELDB#;
280     PRINT USING "#.########";LEVELFACTOR#
290     PRINT #1,"               DC.W    ";
300     PRINT #1,USING "#####           ";SCALEDFACTOR#;
310     PRINT #1,"; ";
320     PRINT #1,USING "##   ";LEVELSETTING%; 
330     PRINT #1,USING "##.#   ";LEVELDB#;
340     PRINT #1,USING "#.########";LEVELFACTOR#
350     NEXT LEVELSETTING% 
360     CLOSE #1
#1,USING "##.#   ";LEVELDB#;
340     PRINT #1,USING "#.########";LEVELFACTOR#
350     NEXT 